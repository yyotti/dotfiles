export LANG=ja_JP.UTF-8
export LESSCHARSET=utf-8
export JLESSCHARSET=japanese-utf-8
export PAGER='less -isr'
# export LSCOLORS=gxfxcxdxbxegedabagacad
export TERM=xterm-256color
export WORDCHARS=${WORDCHARS:s/\///}

# `brew shellenv` is too late.
if [[ -d "${HOME}/.linuxbrew" ]]; then
  export HOMEBREW_PREFIX="${HOME}/.linuxbrew"
elif [[ -d /home/linuxbrew/.linuxbrew ]]; then
  export HOMEBREW_PREFIX="/home/linuxbrew/.linuxbrew"
fi

typeset -U path cdpath fpath manpath

if [[ ${HOMEBREW_PREFIX} != '' ]]; then
  export HOMEBREW_CELLAR="${HOMEBREW_PREFIX}/Cellar"
  export HOMEBREW_REPOSITORY="${HOMEBREW_PREFIX}/Homebrew"
  export MANPATH="${HOMEBREW_PREFIX}/share/man:${MANPATH}"
  export INFOPATH="${HOMEBREW_PREFIX}/share/info:${INFOPATH}"

  path=(
  ${HOMEBREW_PREFIX}/bin(N-/)
  ${HOMEBREW_PREFIX}/sbin(N-/)
  ${path}
  )
fi

path=(
~/.bin(N-/)
~/.local/bin(N-/)
${XDG_CONFIG_HOME}/composer/vendor/bin(N-/)
${DOTFILES}/scripts/git/subcommands(N-/)
${path}
)

if (( ${+commands[vim]} )); then
  export EDITOR=vim
fi

if (( ${+commands[go]} )); then
  export GOPATH=${XDG_DATA_HOME}/go
  path=(${GOPATH}/bin(N-/) ${path})
fi

if [[ -f "${HOME}/.opt/wcwidth-cjk/lib/wcwidth-cjk.so" ]]; then
  export LD_PRELOAD="${HOME}/.opt/wcwidth-cjk/lib/wcwidth-cjk.so"
fi

if (( ${+commands[tig]} )); then
  export TIGRC_USER=${XDG_CONFIG_HOME}/tig/tigrc
fi

if (( ${+commands[less]} )); then
  export LESSHISTFILE=${XDG_CACHE_HOME}/less/history
  export LESSKEY=${XDG_CONFIG_HOME}/less/key
fi

if (( ${+commands[fzf]} )); then
  if (( ${+commands[tmux]} )); then
    export FZF_TMUX=1
  fi

  if (( ${+commands[rg]} )); then
    export FZF_DEFAULT_COMMAND="command rg --files --glob '!.git' --hidden --follow"
    export FZF_CTRL_T_COMMAND="command rg --files --glob '!.git' --hidden --follow"
  elif (( ${+commands[ag]} )); then
    export FZF_DEFAULT_COMMAND="command ag --follow --nocolor --nogroup -g ''"
    export FZF_CTRL_T_COMMAND="command ag --follow --nocolor --nogroup -g ''"
  elif (( ${+commands[pt]} )); then
    export FZF_DEFAULT_COMMAND='command pt --follow --nocolor --nogroup --hidden --ignore=.git -g='
    export FZF_CTRL_T_COMMAND='command pt --follow --nocolor --nogroup --hidden --ignore=.git -g='
  else
    export FZF_DEFAULT_COMMAND=
    export FZF_CTRL_T_COMMAND=
  fi
fi

# skip_global_compinit=1
